//@version=5
indicator("Extreme Direction 5m", overlay=true)

plotcandle(open, high, low, close, 
     color=close > open ? color.new(color.green, 70) : color.new(color.red, 70),
     wickcolor=color.new(color.gray, 70),
     bordercolor=color.new(color.gray, 70))

// === INPUTS ===
// Trend filter for 4h trade window, sensitive to last 2h on 5m bars:
// period1=6 (30min), period2=12 (1h), period3=24 (2h), lookback=12 (1h)
// warmup = period3 + lookback = 36 bars
period1 = input.int(6, "Period 1")
period2 = input.int(12, "Period 2")
period3 = input.int(24, "Period 3")
countLookback = input.int(12, "Lookback")
minBalanceForTrend = input.int(2, "Min Balance to Change Trend")

// === EXTREMES DETECTION ===
h100 = ta.highest(high, period1)
l100 = ta.lowest(low, period1)
h200 = ta.highest(high, period2)
l200 = ta.lowest(low, period2)
h300 = ta.highest(high, period3)
l300 = ta.lowest(low, period3)

isHigh = (high == h100 ? 1 : 0) + (high == h200 ? 1 : 0) + (high == h300 ? 1 : 0)
isLow = (low == l100 ? 1 : 0) + (low == l200 ? 1 : 0) + (low == l300 ? 1 : 0)

// === COUNT ===
totalHighs = math.sum(isHigh, countLookback)
totalLows = math.sum(isLow, countLookback)
balance = totalHighs - totalLows

// === TREND С ПОРОГОМ (гистерезис) ===
var int trend = 0

if balance >= minBalanceForTrend
    trend := 1   // бычий
else if balance <= -minBalanceForTrend
    trend := -1  // медвежий
// иначе trend остаётся прежним

trendColor = trend == 1 ? color.green : trend == -1 ? color.red : color.gray

// === PLOT ===
plot(close, "Close", color=trendColor, linewidth=4)

plotshape(high == h100, "H100", shape.triangleup, location.abovebar, color.aqua, size=size.tiny)
plotshape(low == l100, "L100", shape.triangledown, location.belowbar, color.fuchsia, size=size.tiny)

// === OUTPUTS FOR BOT ===
plot(balance, "Balance", display=display.data_window)
plot(trend, "Trend", display=display.data_window)
plot(close, "Close", display=display.data_window)
plot(totalHighs, "TotalHighs", display=display.data_window)
plot(totalLows, "TotalLows", display=display.data_window)
plot(isHigh, "IsHigh", display=display.data_window)
plot(isLow, "IsLow", display=display.data_window)
